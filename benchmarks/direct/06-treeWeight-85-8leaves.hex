% Declarative encoding 

%Part 1. General declaration
position(X) :- X >= 0, X<N, #int(X), num(N).
coloredPos(X) :- X >= 1, X<N, #int(X), num(N).
leaf(L) :- leafWeightCardinality(L,W,C).

% Assign each leaf a position
leafPos(L,P) v not_leafPos(L,P) :- position(P), leaf(L).
:- leafPos(L,N), leafPos(L,N2), N != N2.
:- leafPos(L,N), leafPos(L2,N), L != L2.
someLeafPosition(L) :- leafPos(L,P).
:- not someLeafPosition(L), leaf(L).

%Part 2: Assign each position a color
posColor(P,C) v not_posColor(P,C) :- color(C), coloredPos(P).
:- posColor(P,C), posColor(P,C2), C != C2.
somePosColor(P) :- posColor(P,C).
:- not somePosColor(P), coloredPos(P).

%Part 3, Cost of each position : red, blue, green colors in order
posCost(0, Cost) :- leafWeightCardinality (L, Cost, C), leafPos(L, 0).

posCost(P, Cost) :- coloredPos(P), posColor(P, red), leafPos(L, P), leafWeightCardinality (L, W, C), PreP = P - 1, posCost(PreP, PreCost), Cost = PreCost + W.
posCost(P, Cost) :- coloredPos(P), posColor(P, green), leafPos(L, P), leafWeightCardinality (L, W, C), PreP = P - 1, posCost(PreP, PreCost), Cost = PreCost + C.
posCost(P, Cost) :- coloredPos(P), posColor(P, blue), leafPos(L, P), leafWeightCardinality (L, W, C), PreP = P - 1, posCost(PreP, PreCost), Cost = W + C.

%Part 4. Get total cost
seqCost(1, Cost) :- posCost(1, Cost).
seqCost(P, Cost) :- coloredPos(P), P > 1, PreP = P-1, seqCost(PreP,C), posCost(P,PreC), Cost = C + PreC.

%Part 5
exists :- PreN = N-1, seqCost(PreN, Cost), num(N), Cost <= M, max_total_weight(M).
:- not exists.
leafWeightCardinality(1,5,8).
leafWeightCardinality(2,5,1).
leafWeightCardinality(3,10,9).
leafWeightCardinality(4,1,8).
leafWeightCardinality(5,5,9).
leafWeightCardinality(6,3,14).
leafWeightCardinality(7,8,9).
leafWeightCardinality(8,12,13).
innerNode(1).
innerNode(2).
innerNode(3).
innerNode(4).
innerNode(5).
innerNode(6).
innerNode(7).
num(8).
#maxint=8.
max_total_weight(85).
color(red).
color(green).
color(blue).
